import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

group 'dev.josemc.essentials'
version pluginVersion

repositories {
    mavenCentral()

    // PaperMC Repo
    maven { url = "https://repo.papermc.io/repository/maven-public/" }

    // JitPack
    maven { url = 'https://jitpack.io' }
}

dependencies {
    // PaperMC
    compileOnly("io.papermc.paper:paper-api:${mcVersion}-${paperApi}")

    // SpongeForge Configurate
    implementation("org.spongepowered:configurate-hocon:${configurateVersion}")

    // Lamp Command Framework
    implementation("com.github.Revxrsal.Lamp:common:${lampVersion}")
    implementation("com.github.Revxrsal.Lamp:bukkit:${lampVersion}")
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: pluginVersion]
    }
}

shadowJar {
    relocate("org.spongepowered.configurate", "dev.josemc.essentials.lib.configurate")
    relocate("com.typesafe", "dev.josemc.essentials.lib.typesafe")
    relocate("io.leangen.geantyref", "dev.josemc.essentials.lib.geantyref")
    relocate("revxrsal.commands", "dev.josemc.essentials.lib.commands")
}

task testJar(type: Copy) {
    dependsOn(':clean')
    from shadowJar
    into(project.file("./TestServer/plugins"))
}
